mod sort

use "std/libc" @libc
use "std/runtime" {panic}
use "std/types/list" {List}
use "std/specs" {Ord, Order}
use "std/allocator" {LibcAllocator}

fn main() {
    // Created a list and populate it.
    let mut al = LibcAllocator.new().dynamic()
    let mut list = List[Value].new(al)
    for let mut i = 10; i >= 0; i -= 1 {
        list.push(Value{v: i})
    }

    // Sort the list.
    sort(&mut list)

    // Check that the list is sorted correctly.
    if !is_sorted(&list) {
        panic("list is not sorted")
    }

    @libc.exit(0)
}

/// Just contains an int and can be ordered.
struct Value {
    v: int
}

impl Value: Order {
    fn compare(self, other: *Value) -> Ord {
        if self.v > other.v {
            return Ord::Greater
        } else if self.v < other.v {
            return Ord::Lesser
        }
        return Ord::Equal
    }
}

/// Returns true only if the list is sorted in ascending order.
fn is_sorted[T: Order](list: *List[T]) -> bool {
    for let mut i = 0uint; i < list.len() - 1; i += 1 {
        let ival = list.get(i).unwrap()
        let jval = list.get(i + 1).unwrap()
        if ival.compare(&jval) ~~ Ord::Greater {
            return false
        }
    }

    return true
}

/// Sorts the list in ascending order.
fn sort[T: Order](list: *mut List[T]) {
    for let mut i = 0uint; i < list.len() - 1; {} {
        let cur_value = list.get(i).unwrap()
        let result = find_min(list, i + 1)
        let min_index = result.(0)
        let min_value = result.(1)
        if min_value.compare(&cur_value) ~~ Ord::Lesser {
            list.replace(i, min_value)
            list.replace(min_index, cur_value)
        } else {
            i += 1
        }
    }
}

/// Finds the minimum value from the `start` index to the end of the list (inclusive).
fn find_min[T: Order](list: *List[T], start: uint) -> {uint, T} {
    let mut min_value = list.get(start).unwrap()
    let mut min_index = start

    for let mut i = start + 1; i < list.len(); i += 1 {
        let v = list.get(i).unwrap()
        if v.compare(&min_value) ~~ Ord::Lesser {
            min_value = v
            min_index = i
        }
    }

    return {min_index, min_value}
}