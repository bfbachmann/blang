mod mem

use "std/libc" @libc

/// Allocates space on the heap for `count` values of type `T` and returns a pointer
/// to the allocated region.
pub fn heap_alloc[T](count: uint) -> *mut T {
    return @libc.calloc(sizeof T, count) as *mut T
}

/// Frees the heap space pointed to by `ptr`.
pub fn heap_free[T](ptr: *mut T) {
    @libc.free(ptr as *mut u8)
}

/// Reallocates the heap space referenced by `ptr` so it can store `count` values
/// of type `T`. This will also copy the existing data into the new memory region.
pub fn heap_realloc[T](ptr: *mut T, count: uint) -> *mut T {
    return @libc.realloc(ptr as *mut u8, sizeof T * count) as *mut T
}

/// Copies a `T` from `src` to `dst`. These two pointers must not point to overlapping
/// regions.
pub fn copy[T](dst: *mut T, src: *T) {
    @libc.memcpy(dst as *mut u8, src as *u8, sizeof T)
}

/// Moves `T` onto the heap and returns a pointer to it.
pub fn heap[T](t: T) -> *mut T {
    let ptr: *mut T = heap_alloc(1)
    ptr^ = t
    return ptr
}

/// Replaces the value pointed to by `dst` with `value` and returns the data
/// pointed to by `dst`.
pub fn replace[T](dst: *mut T, mut value: T) -> T {
    swap(dst, &mut value)
    return value
}

/// Swaps the data pointed to by `a` and `b`.
pub fn swap[T](a: *mut T, b: *mut T) {
    let a_copy = a^
    a^ = b^
    b^ = a_copy
}