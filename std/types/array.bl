use panic: "std/backtrace/panic.bl"
use {Clone}: "std/specs/clone.bl"
use {DynAllocator}: "std/mem/dyn_allocator.bl"
use {SliceMut}: "std/types/slice.bl"

/// A resizeable array of values of type T.
pub struct Array[T] {
    allocator: *mut DynAllocator
    data: SliceMut[T]
}

impl Array {
    /// Create a new empty array.
    pub fn new(al: *mut DynAllocator) -> Array[T]{
        return Array[T].new_with_len(al, 0)
    }

    /// Create a new array that can contain `len` elements.
    pub fn new_with_len(al: *mut DynAllocator, len: uint) -> Array[T] {
        let mut ptr: *mut T = null
        if len > 0 {
            ptr = al.alloc(sizeof T * len) as *mut T
        }

        return Array[T]{
            allocator: al
            data: SliceMut[T].new(ptr, len)
        }
    }

    /// Resize the array so it can hold `len` elements. Be careful with 
    /// this function because it might move all the data in the array,
    /// which would invalidate any existing pointers to its data.
    pub fn resize(*mut self, len: uint) {
        let ptr = self^.allocator.realloc(
            self^.data.ptr() as *mut u8, 
            sizeof T * len,
        ) as *mut T
        self^.data = SliceMut[T].new(ptr, len)
    }

    /// Returns a pointer to the element at index `i`. Panics if `i`
    /// is out of bounds.
    pub fn ptr_at(*self, i: uint) -> *T {
        return self^.data.ptr_at(i)
    }

    /// Returns a mutating pointer to the element at index `i`. Panics if
    /// `i` is out of bounds.
    pub fn mut_ptr_at(*mut self, i: uint) -> *mut T {
        return self^.data.ptr_at(i)
    }

    /// Returns the nubmer of elements in the array.
    pub fn len(*self) -> uint {
        return self^.data.len()
    }

    /// Frees all heap memory allocated for the array.
    pub fn free(mut self) {
        self.allocator.free(self.data.ptr() as *u8)
    }
}

impl Array: Clone {
    /// Returns a shallow clone of the array (i.e. does not clone entries).
    fn clone(*self) -> Array[T] {
        return Array[T]{
            allocator: self^.allocator
            data: self^.data.clone()
        }
    }
}