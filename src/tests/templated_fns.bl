extern fn exit(code: i64)

fn assert(b: bool, code: i64) {
    if !b {
        exit(code)
    }
}

fn apply(f: F, arg: T) ~ T
with [
    T,
    F = fn (T) ~ T,
] {
    return f(arg)
}

fn double_if(v: T, b: B) ~ i64
with [T = i64, B = bool]
{
    if b {
        return v * 2
    }
    return v
}

fn double(v: i64) ~ i64 {
    return v * 2
}

spec Task {
    fn run(this) ~ bool
}

struct BasicTask {
    succeed: bool
}

impl BasicTask {
    fn new(succeed: bool) ~ BasicTask {
        return BasicTask{succeed: succeed}
    }

    fn run(this) ~ bool {
        return this.succeed
    }
}

fn run_task(task: T) ~ bool
with [T: Task]
{
    return task.run()
}

fn do_nothing(a: T, b: T) with [T] {}

impl i64 {
    fn zero() ~ i64 {
        return 0
    }
}

spec Zero {
    fn zero() ~ i64
}

fn is_zero_value(v: T) ~ bool
with [T: Zero] {
    return v == T.zero()
}

fn main() {
    // Check that template parameters work in place of concrete types.
    {
        assert(apply(double, 2) == 4, 1)
        assert(double_if(5, true) == 10, 2)
    }

    // Check that template parameters work in place of specs.
    {
        let task = BasicTask.new(true)
        assert(run_task(task) == true, 3)
    }

    // Check that functions with arguments that share template parameters
    // work.
    {
        do_nothing(1, 2)
        do_nothing("another", "test")
    }

    // Check that we can use templated methods that call type member functions
    // on our templated types.
    {
        assert(is_zero_value(0), 4)
    }

    exit(0)
}