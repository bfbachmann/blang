use {exit}: "std/libc/proc.bl"

use {assert}: "src/tests/test_utils.bl"

fn cumulative_sum(n: i64): i64 {
    let mut result: i64 = 0
    for let mut i = n, i > 0, i -= 1 {
        result = result + i
    }

    return result
}

fn fib(n: i64): i64 {
    if n <= 2 { 
        return 1
    }
    return fib(n-1) + fib(n-2)
}

fn run_fn_with_arg(f: fn (i64): i64, arg: i64): i64 {
    return f(arg)
}

fn double(i: i64): i64 {
    return i * 2
}

struct CalcTask {
    f: fn (i64): i64
    arg: i64
}

impl CalcTask {
    fn run(self): i64 {
        return self.f(self.arg)
    }
}

fn get_sum_fn(): fn (int, int): int {
    fn sum(a: int, b: int): int {
        return a + b
    }

    return sum
}

fn main() {
    // Check that regular function calls work.
    {
        assert(cumulative_sum(50) == 1275, "expected sum to be 1275")
        assert(fib(10) == 55, "expected fib(10) to be 55")
    }

    // Check that functions can be passed like any other data and called from
    // variables.
    {
        assert(run_fn_with_arg(double, 2) == 4, "expected 4")

        let task = CalcTask{
            f: double
            arg: -5
        }
        assert(task.run() == -10, "expected task to return -10")
    }

    // Check that nested functions are usable.
    {
        fn mul(val_ptr: *mut int, factor: int) {
            val_ptr^ = val_ptr^ * factor
        }

        let mut x = 11
        mul(&mut x, 2)
        assert(x == 22, "expected 22")

        let f = get_sum_fn()
        assert(f(3, 4) == 7, "expected 7")
    }

    // Check that anonymous functions work.
    {
        let f = fn (): int {
            fn get_43(): int {
                return 43
            }
            return get_43() - 1
        }
        assert(f() == 42, "expected anon fn to return 42")
        assert(
            fn (i: int): int {
                return i * 2
            }(-3) == -6,
            "expected inline anonymous function call to return -6",
        )
    }

    // Check that methods can be used as variables, just like regular
    // functions.
    {
        let method = CalcTask.run
        let task = CalcTask{
            f: double
            arg: 2
        }
        assert(method(task) == 4, "expected indirect method call to return 4")
    }

    exit(0)
}
