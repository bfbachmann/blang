use "test_utils.bl"

fn cumulative_sum(n: i64) ~ i64 {
    let mut result = 0
    for let mut i = n; i > 0; i = i - 1;
        result = result + i

    return result
}

fn fib(n: i64) ~ i64 {
    if n <= 2 return 1
    return fib(n-1) + fib(n-2)
}

fn run_fn_with_arg(f: fn (i64) ~ i64, arg: i64) ~ i64
    return f(arg)

fn double(i: i64) ~ i64 
    return i * 2

struct CalcTask {
    f: fn (i64) ~ i64
    arg: i64
}

impl CalcTask {
    fn run(self) ~ i64
        return self.f(self.arg)
}

fn main() {
    // Check that regular function calls work.
    {
        assert(cumulative_sum(50) == 1275, 1)
        assert(fib(10) == 55, 2)
    }

    // Check that functions can be passed like any other data and called from
    // variables.
    {
        assert(run_fn_with_arg(double, 2) == 4, 3)

        let task = CalcTask{
            f: double
            arg: -5
        }
        assert(task.run() == -10, 4)
    }

    exit(0)
}
